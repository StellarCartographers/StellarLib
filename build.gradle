plugins {
	id 'org.cadixdev.licenser' version '0.6.1'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'space.tscg.common'
version '0.1.0'

println('Version: ' + version +
        ' Java: ' + System.getProperty('java.version') +
        ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ')' +
        ' Arch: ' + System.getProperty('os.arch'))

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation 'org.slf4j:slf4j-api:2.0.7'
    api('com.rethinkdb:rethinkdb-driver:2.4.4') {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    }
    api 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
    api 'com.github.ROMVoid95:EliteDangerousAPI:-SNAPSHOT'
    
    compileOnly 'org.projectlombok:lombok:1.18.28'
	annotationProcessor 'org.projectlombok:lombok:1.18.28'
	testCompileOnly 'org.projectlombok:lombok:1.18.28'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.28'
}

license{
	header = file("$rootDir/LICENSE-header.txt")
    properties {
        GROUP = 'The Stellar Cartographers\' Guild'
        YEAR = Calendar.getInstance().get(Calendar.YEAR);
    }
    newLine = false
    ignoreFailures = true
    style { java = 'JAVADOC' }
}

java {
	withJavadocJar()
	withSourcesJar()
	toolchain.languageVersion = JavaLanguageVersion.of(17)
}

tasks.withType(Javadoc) {
      failOnError false
      options.addStringOption('Xdoclint:none', '-quiet')
      options.addStringOption('encoding', 'UTF-8')
      options.addStringOption('charSet', 'UTF-8')
}

artifacts {
	archives jar
	archives javadocJar
	archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'space.tscg.common'
            artifactId 'stellarlib'
            version version

			from components.java
            
            pom {
                name = project.name
                description = 'common library for all TSCG projects'
                url = 'https://github.com/StellarCartographers/StellarLib'

                scm {
                    url = 'https://github.com/StellarCartographers/StellarLib'
                    connection = 'scm:git:git://github.com/StellarCartographers/StellarLib.git'
                    developerConnection = 'scm:git:git@github.com:StellarLib/StellarLib.git'
                }

                issueManagement {
                    system = 'github'
                    url = 'https://github.com/StellarCartographers/StellarLib/issues'
                }

                licenses {
                    license {
                        name = 'GPL-3.0'
                        url = 'https://github.com/StellarCartographers/StellarLib/blob/master/LICENSE'
                        distribution = 'repo'
                    }
                }
            }
        }
    }
    signing {
		publishing.publications.each {
		    sign it
		}
    }
}