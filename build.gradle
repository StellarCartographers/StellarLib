import space.tscg.gradle.Shared

plugins {
    id "com.autonomousapps.dependency-analysis" version "1.29.0"
    id 'com.diffplug.spotless' version '6.+'
    id 'io.freefair.lombok' version '8.+'
    id 'tscg.defaults' version '2.2.2'
    id 'java-library'
}

group 'space.tscg'
version '1.0.0-SNAPSHOT'

tscg {
    devName 'ROMVoid95'
    devEmail 'rom.void95@gmail.com'
    onCentral true
}

spotless {
    ratchetFrom 'origin/master'

    format('misc') {
        target '*.gradle', '.gitattributes', '.gitignore'
        targetExclude('**/build/**/*.*')
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
    java {
        licenseHeaderFile rootProject.file('extra/spotless/license.txt')
        importOrderFile rootProject.file('extra/eclipse/eclipse.importorder')
        eclipse().configFile rootProject.file('extra/eclipse/eclipse-formatter.xml')
    }
}

dependencies {
    api Shared.Deps().JacksonCore()
    api Shared.Deps().JacksonAnnotations()
    api Shared.Deps().JacksonDatabind()
    api Shared.Deps().RethinkDb()
    api Shared.Deps().Expressible()
    
    compileOnly Shared.Libs().Elite4J()
    compileOnly Shared.Deps().OAuthSdk()
    compileOnly Shared.Deps().Javalin()
    compileOnly Shared.Deps().OkHttp3()
    compileOnly Shared.Deps().TinyLogApi()
    compileOnly('net.dv8tion:JDA:latest.integration') { exclude module: 'opus-java' }
    
    implementation Shared.Deps().Environment()
    implementation Shared.Deps().CommonsLang3()
    implementation 'commons-validator:commons-validator:1.7'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:latest.release'
}

javadoc.options.addStringOption('Xdoclint:none', '-quiet')

jar {
    manifest {
        attributes([
            'Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            'GitCommit': tscg.git.abbreviatedId,
            'Git-Branch': tscg.git.branch
        ] as LinkedHashMap)
        attributes([
            'Specification-Title':      'StellarAPI',
            'Specification-Vendor':     'TSCG',
            'Specification-Version':    '1',
            'Implementation-Title':     'StellarAPI Server',
            'Implementation-Version':   project.version,
            'Implementation-Vendor':    'StellarCartographers'
        ] as LinkedHashMap, 'space/tscg/')
    }
}
